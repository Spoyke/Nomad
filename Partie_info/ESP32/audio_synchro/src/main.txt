#include <Arduino.h>
#include <WiFi.h>
#include <WiFiUdp.h>
#include <WebSocketsClient.h>
#include "MyWebSocket.h"
#include "MyI2S.h"

// --- CONFIGURATION RÉSEAU ---
#define NETWORK 4
#if NETWORK == 4
  const char ssid[] = "MonHotspot";
  const char password[] = "axel1234";
#endif

const char url[] = "http://10.42.0.1:8000/streamright";

// --- CONFIGURATION UDP ---
WiFiUDP udp;
const unsigned int localUdpPort = 12345;
uint8_t packetBuffer[2048];

// --- VARIABLES DE SYNCHRO ---
uint32_t targetEpochMillis = 0;
bool hasTargetTime = false;

// --- CALLBACK WEBSOCKET ---
void webSocketEvent(WStype_t type, uint8_t * payload, size_t length) {
    switch(type) {
        case WStype_TEXT: {
            // Serial.printf("WS Reçu : %s\n", (char*)payload);
            char target[8], command[16], content[32];
            if (!extractField((char*)payload, "target", target, sizeof(target))) return;
            
            if (strcmp(target, "esp32") == 0) {
                extractField((char*)payload, "command", command, sizeof(command));
                extractField((char*)payload, "content", content, sizeof(content));

                if (strcmp(command, "Sync") == 0) {
                    targetEpochMillis = atoll(content);
                    hasTargetTime = true;
                } else if (strcmp(command, "Stop") == 0) {
                    stopAudio();
                } else if (strcmp(command, "Volume") == 0) {
                    setVolumeLevel(atoi(content));
                }
            }
        } break;
        case WStype_DISCONNECTED: Serial.println("WS Déconnecté"); break;
        case WStype_CONNECTED: Serial.println("WS Connecté"); break;
        default: break;
    }
}

void setup() {
    Serial.begin(115200);
    
    // 1. Connexion WiFi
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }
    Serial.println(WiFi.localIP());
    Serial.println("\nWiFi OK");

    // 2. Initialisation Audio (Pins 10, 11, 12)
    Audio_init();
    
    // 3. Initialisation Services
    WS_init(webSocketEvent);  
    udp.begin(localUdpPort);
    
    Serial.println("Serveur UDP et WebSocket prêts.");
}

void loop() {
    WS_loop();     
    Audio_loop();  

    int packetSize = udp.parsePacket();
    if (packetSize) {
        // On lit le paquet
        int len = udp.read(packetBuffer, 2048); 
        
        if (len > 0) {
            // Détection du signal de config "salut"
            if (len >= 5 && memcmp(packetBuffer, "salut", 5) == 0) {
                Serial.println(">>> Mode MICRO activé (16kHz)");
                stopAudio(); 
                i2s_stop(I2S_NUM_0);
                // Utilise STEREO si le MAX98357 ne sort rien en MONO
                i2s_set_clk(
    I2S_NUM_0,
    16000,
    I2S_BITS_PER_SAMPLE_16BIT,
    I2S_CHANNEL_STEREO
);

                i2s_zero_dma_buffer(I2S_NUM_0);
                i2s_start(I2S_NUM_0);
            } 
            // Si on n'est pas en train de lire une musique Web, on joue l'UDP
            else if (!audio.isRunning()) {
    if (len % 4 != 0) return; // 4 octets = 1 sample stéréo
    playUdpChunk(packetBuffer, len);
}

        }
    }

    if (hasTargetTime) {
        startAudio(url);
        hasTargetTime = false;
    }
}